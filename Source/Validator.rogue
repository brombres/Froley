library Froley

class Validator : Visitor
  METHODS
    method on_visit_node( cmd:Collect )
      if (cmd.args.count == 0) throw cmd.args.t.error( "One or more arguments expected." )
      visit_children( cmd )

    method on_visit_node( cmd:Print )
      if (cmd.args.count == 0)
        throw cmd.t.error( "Argument expected for 'print'." )
      endIf
      visit_children( cmd )

    method on_visit_node( cmd:ProgramDef )
      forEach (type in Program.types)
        if (not type.is_defined)
          throw type.t.error( "Use of undefined node type '$'."(type.name) )
        endIf
        if (type.is_node and not type.base_type) type.@base_type = Program.type_Cmd
      endForEach
      visit_children( cmd )

    method on_visit_node( cmd:ParserDef )
      if (cmd.vars.count)
        forEach (v in cmd.vars)
          if (v.type == FType.NONE)
            throw v.t.error( "Reference to undefined routine or variable '$'."(v.name) )
          endIf
        endForEach
      endIf
      visit_children( cmd )

    method on_visit_node( cmd:ScannerDef )
      if (cmd.vars.count)
        forEach (v in cmd.vars)
          if (v.type == FType.NONE)
            throw v.t.error( "Reference to undefined routine or variable '$'."(v.name) )
          endIf
        endForEach
      endIf
      visit_children( cmd )
endClass

