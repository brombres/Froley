module Simple

class Resolver : Visitor
  METHODS
    method require_value( t:Token, cmd:Cmd )->Cmd
      if (cmd) return cmd.require_value
      throw t.error( "Value expected." )

    method on( cmd:Assign )->Cmd
      return visit( cmd.left.resolve_assignment(this,cmd.right) )

    method on( cmd:Access )->Cmd
      return visit( ReadVar(cmd.t,cmd.name) )

    method on_visit( cmd:Binary )
      visit_children( cmd )
      require_value( cmd.t, cmd.left )
      require_value( cmd.t, cmd.right )

    method on_visit( cmd:Unary )
      visit_children( cmd )
      require_value( cmd.t, cmd.operand )

    method on_visit( cmd:Args )
      visit_children( cmd )
      forEach (arg in cmd) require_value(cmd.t,arg)

    method on( cmd:Add )->Cmd
      visit_children( cmd )
      if (cmd.left instanceOf Number and cmd.right instanceOf Number)
        return visit( Number(cmd.t, cmd.left->Real64 + cmd.right->Real64) )
      else
        return cmd
      endIf

    method on( cmd:Subtract )->Cmd
      visit_children( cmd )
      if (cmd.left instanceOf Number and cmd.right instanceOf Number)
        return visit( Number(cmd.t, cmd.left->Real64 - cmd.right->Real64) )
      else
        return cmd
      endIf

    method on( cmd:Multiply )->Cmd
      visit_children( cmd )
      if (cmd.left instanceOf Number and cmd.right instanceOf Number)
        return visit( Number(cmd.t, cmd.left->Real64 * cmd.right->Real64) )
      else
        return cmd
      endIf

    method on( cmd:Divide )->Cmd
      visit_children( cmd )
      if (cmd.left instanceOf Number and cmd.right instanceOf Number)
        return visit( Number(cmd.t, cmd.left->Real64 / cmd.right->Real64) )
      else
        return cmd
      endIf

    method on( cmd:Power )->Cmd
      visit_children( cmd )
      if (cmd.left instanceOf Number and cmd.right instanceOf Number)
        return visit( Number(cmd.t, cmd.left->Real64 ^ cmd.right->Real64) )
      else
        return cmd
      endIf

    method on_resolve( cmd:Number )->Cmd
      return cmd
endClass

augment
  METHODS
    method Cmd.require_value->Cmd
      throw t.error( "Value expected." )

    method Cmd.resolve_assignment( resolver:Resolver, new_value:Cmd )->Cmd
      throw t.error( "Illegal assignment target." )

    method Unary.require_value->Cmd
      return this

    method Binary.require_value->Cmd
      return this

    method Number.require_value->Cmd
      return this

    method LiteralString.require_value->Cmd
      return this

    method ReadVar.require_value->Cmd
      return this

    method Access.resolve_assignment( resolver:Resolver, new_value:Cmd )->Cmd
      return resolver.visit_node( WriteVar(t,name,new_value) )
endAugment

