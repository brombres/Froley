module F3

$include "RCmd.rogue"
$include "RFrameStack.rogue"
$include "RMain.rogue"
$include "RParser.rogue"
$include "RParserCore.rogue"
$include "RScanTable.rogue"
$include "RScanner.rogue"
$include "RScannerCore.rogue"
$include "RSyntaxError.rogue"
$include "RToken.rogue"
$include "RTokenType.rogue"
$include "RVisitor.rogue"
$include "RogueSourceWriter.rogue"
$include "ScanTableDeclarationWriter.rogue"

class RogueGenerator : Visitor
  GLOBAL METHODS
    method to_escaped_ascii( st:String, additional_escapes:String )->String
      return st.to_escaped_ascii( additional_escapes )

  PROPERTIES
    folder     : String
    writer     : PrintWriter
    class_name = ""

    indent     = 0
    at_newline = true
    write_replacements = StringTable<<String>>()

  METHODS
    method init( folder )
      File.create_folder( folder )

      Program.nodes.dispatch( this )

      RSyntaxError(this).create_missing
      RScanTable(this).create_missing
      RTokenType(this).overwrite
      RToken(this).create_missing
      RFrameStack(this).create_missing

      # Main
      if (Program.main_filename)
        RMain( this, Program.main_filename ).create_missing
      endIf

    method on( cmd:BeginList )
      write   "_begin_list"

    method on( cmd:Call )
      if (is_scanner)
        write "if (not _execute(ip_"
        write cmd.routine_def.name
        write ")) return false"
      else
        write   "_execute(ip_"
        write   cmd.routine_def.name
        writeln ")"
        write   "_pop_frame"
      endIf

    method on( cmd:CastToLogical )
      cmd.operand.dispatch( this )
      write "->Logical"

    method on( cmd:CastToCharacter )
      cmd.operand.dispatch( this )
      write "->Character"

    method on( cmd:CastToInteger )
      cmd.operand.dispatch( this )
      write "->Int32"

    method on( cmd:CastToString )
      cmd.operand.dispatch( this )
      write "->String"

    method on( cmd:CharSet )
      trace cmd.characters
      println StackTrace()

    method on( cmd:Collect )
      write "buffer"
      forEach (arg in cmd.args)
        write ".print("
        arg.dispatch( this )
        write ")"
      endForEach

    method on( cmd:CompareEQ )
      write "("
      dispatch( cmd.left )
      write "=="
      dispatch( cmd.right )
      write ")"

    method on( cmd:CompareGE )
      write "("
      dispatch( cmd.left )
      write ">="
      dispatch( cmd.right )
      write ")"

    method on( cmd:CompareGT )
      write "("
      dispatch( cmd.left )
      write ">"
      dispatch( cmd.right )
      write ")"

    method on( cmd:CompareLE )
      write "("
      dispatch( cmd.left )
      write "<="
      dispatch( cmd.right )
      write ")"

    method on( cmd:CompareLT )
      write "("
      dispatch( cmd.left )
      write "<"
      dispatch( cmd.right )
      write ")"

    method on( cmd:CompareNE )
      write "("
      dispatch( cmd.left )
      write "!="
      dispatch( cmd.right )
      write ")"

    method on( cmd:ConsumeCharacters )
      write "_scanner.consume("
      dispatch( cmd.value )
      write ")"

    method on( cmd:ConsumeContent )
      write "_consume_content("
      write ''"$"'' (to_escaped_ascii(cmd.content,"\""))
      write ")"

    method on( cmd:ConsumeEOLs )
      write "_consume_eols"

    method on( cmd:ConsumePattern )
      dispatch( cmd.pattern )
      write ".scan(_scanner)"

    method on( cmd:ConsumeToken )
      write "_consume(TokenType."
      write cmd.token_type.name
      write ")"

    method on( cmd:CreateCmd )
      write "_push( "
      write cmd.name
      write "(_cur_t"
      cmd.args.dispatch( this )
      write "), "
      write cmd.node_arg_count
      write " )"

    method on( cmd:CreateCmdArg )
      write ","
      local arg_type = Program.type( cmd.t, cmd.arg_type )
      if (cmd.arg_value)
        if (arg_type.is_built_in and arg_type.name != "String")
          write "_string_to_$(" (arg_type.name)
          cmd.arg_value.dispatch( this )
          write ")"
        else
          cmd.arg_value.dispatch( this )
        endIf
      else
        if (arg_type.is_built_in)
          write "_$_arg($)"(arg_type.name_lc,cmd.stack_offset)
        else
          write "_node_arg($)"(cmd.stack_offset)
          if (arg_type.name != "Cmd") write " as "; write arg_type.name
        endIf
      endIf

    method on( cmd:CreateList )
      write "_push_list( $(_list_t) )" (cmd.list_type)

    method on( cmd:CreateNull )
      write "_push( null, 0 )"

    method on( cmd:MustConsumeCharacters )
      write "_must_consume( "
      dispatch( cmd.value )
      write " )"

    method on( cmd:MustConsumePattern )
      write "_must_consume( "
      dispatch( cmd.pattern )
      write " )"

    method on( cmd:MustConsumeToken )
      write "if (not _consume(TokenType."
      write cmd.token_type.name
      write ")) _throw_syntax_error( &expected=TokenType."
      write cmd.token_type.name
      write " )"

    method on( cmd:ParserHasAnother )
      write "(_position < _limit)"

    method on( cmd:Pattern )
      write cmd.reference_name

    method on( cmd:Decrement )
      write "--"
      dispatch( cmd.target )

    method on( cmd:DiscardList )
      write "_discard_list"

    method on( cmd:DiscardPosition )
      if (is_scanner)
        write @|if (_position_stack.count) _position_stack.remove_last
      else
        write "_discard_position"
      endIf

    method on( cmd:If )
      write   "if ("
      dispatch( cmd.condition )
      writeln ")"
      indent += 2
      dispatch( cmd.statements )
      indent -= 2

      dispatch( cmd.else_ifs )

      if (cmd._else)
        writeln "else"
        indent += 2
        dispatch( cmd._else )
        indent -= 2
      endIf

      write   "endIf"

    method on( cmd:ElseIf )
      write   "elseIf ("
      dispatch( cmd.condition )
      writeln ")"
      indent += 2
      dispatch( cmd.statements )
      indent -= 2

    method on( cmd:Halt )
      writeln "halt = true"
      write   "return false"

    method on( cmd:Increment )
      write "++"
      dispatch( cmd.target )

    method on( cmd:ScannerHasAnother )
      write "_scanner.has_another"

    method on( cmd:LiteralCharacter )
      which (cmd.value)
        case '\\': write @|'\\'
        case '\n': write @|'\n'
        case '\r': write @|'\r'
        case '\t': write @|'\t'
        others
          write( "'$'"(to_escaped_ascii(cmd.value,"'")) )
      endWhich

    method on( cmd:LiteralInteger )
      writer.print( cmd.value )

    method on( cmd:LiteralLogical )
      writer.print( cmd.value )

    method on( cmd:LiteralString )
      writer.print( ''"$"''(to_escaped_ascii(cmd.value,''"'')) )
      #if (cmd.value.count == 1)
      #  writer.print( "'$'"(to_escaped_ascii(cmd.value,"'")) )
      #else
      #  writer.print( ''"$"''(to_escaped_ascii(cmd.value,''"'')) )
      #endIf

    method on( cmd:LiteralTokenType )
      write "TokenType."
      write cmd.token_type.name

    method on( cmd:LogicalAnd )
      write "("
      cmd.left.dispatch( this )
      write " and "
      cmd.right.dispatch( this )
      write ")"

    method on( cmd:LogicalNot )
      write "(not "
      cmd.operand.dispatch( this )
      write ")"

    method on( cmd:LogicalOr )
      write "("
      cmd.left.dispatch( this )
      write " or "
      cmd.right.dispatch( this )
      write ")"

    method on( cmd:MarkPosition )
      writeln "_src_line   = _scanner.line"
      write   "_src_column = _scanner.column"

    method on( cmd:Mode )
      write "start_ip = ip_"
      write cmd.routine_name

    method on( cmd:On )
      write   "if (_next_t.type == "
      dispatch( cmd.token_type )
      writeln ")"
      indent += 2
      writeln "_on_t"
      dispatch( cmd.statements )
      indent -= 2

      dispatch( cmd.else_ons )

      if (cmd._else_on_others)
        writeln "else"
        indent += 2
        dispatch( cmd._else_on_others )
        indent -= 2
      endIf

      write   "endIf"

    method on( cmd:ElseOn )
      write   "elseIf (_next_t.type == "
      dispatch( cmd.token_type )
      writeln ")"
      indent += 2
      writeln "_on_t"
      dispatch( cmd.statements )
      indent -= 2

    method on( cmd:ScannerCallUserMethod )
      writeln "this.ip = ip"
      writeln cmd.fn_name
      write   "ip = this.ip"

    method on( cmd:MustConsumeContent )
      write "_must_consume_content("
      write ''"$"'' (to_escaped_ascii(cmd.content, "\""))
      write ")"

    method on( cmd:NextHasAttribute )
      write "(_next_t.type.attributes & TokenType.ATTRIBUTE_$)?"(cmd.name.to_uppercase)

    method on( cmd:NextIsCharacters )
      if (cmd.value.type == FType.CHARACTER)
        write "_scanner.next_is("
        dispatch( cmd.value )
        write ")"
      else
        write "_next_is("
        dispatch( cmd.value )
        write ")"
      endIf

    method on( cmd:NextIsContent )
      write ''(_next_t.content and _next_t.content == "''
      write to_escaped_ascii( cmd.content, "\"" )
      write ''")''

    method on( cmd:NextIsPattern )
      dispatch( cmd.pattern )
      write ".is_next(_scanner)"

    method on( cmd:ParserCallUserMethod )
      writeln "this.ip = ip"
      writeln cmd.fn_name
      write   "ip = this.ip"

    method on( cmd:ParserCreate )
      cmd.arg.dispatch( this )

    method on( cmd:NextIsTokenType )
      write "(_next_t.type == TokenType."
      write cmd.token_type.name
      write ")"

    method on( cmd:Print )
      write "output"
      forEach (arg in cmd.args)
        write ".print("
        arg.require_value.dispatch( this )
        write ")"
      endForEach

    method on( cmd:Println )
      write "output"
      forEach (arg in cmd.args)
        write ".print("
        arg.require_value.dispatch( this )
        write ")"
      endForEach
      writeln ".println"
      write "_on_output_line"

    method on( cmd:ProduceList )
      writeln "_push_list( $(_list_t) )" (cmd.list_type)
      write   "return"

    method on( cmd:ScanCharacters )
      write "_scan("
      dispatch( cmd.value )
      write ")"

    method on( cmd:ScanPatternCmd )
      dispatch( cmd.pattern )
      write ".scan(_scanner,buffer)"

    method on( cmd:ParserRead )
      write "_read"

    method on( cmd:ScannerCreate )
      write   "_add( "
      cmd.arg.dispatch( this )
      writeln " )"

    method on( cmd:ScannerPeek )
      write   "_scanner.peek "
      if (cmd.lookahead)
        write "("
        cmd.lookahead.dispatch( this )
        write ")"
      endIf

    method on( cmd:ScannerRead )
      write "_scanner.read"

    method on( cmd:ReadBuffer )
      write "buffer"

    method on( cmd:ReadVar )
      write cmd.var.name

    method on( cmd:Restart )
      if (cmd.start_ip)
        write "start_ip = ip_"
        writeln cmd.start_ip
      endIf
      write "return false"

    method on( cmd:RestorePosition )
      if (is_scanner)
        write "if (_position_stack.count) _scanner.seek( _position_stack.remove_last )"
      else
        write "_restore_position"
      endIf

    method on( cmd:Return )
      if (is_scanner) write   "return true"
      else            write   "return"

    method visit( cmd:Routine )->Logical [override]
      writeln "          case ip_$"(cmd.name)

      indent += 12
      prior.visit( cmd )

      if (is_parser)
        if (not BranchAnalyzer.all_paths_return(cmd))
          writeln "return"
        endIf
      endIf

      indent -= 12
      return false


    method on( cmd:SavePosition )
      if (is_scanner)
        write "_position_stack.add( _scanner.position )"
      else
        write "_save_position"
      endIf

    method on( cmd:ScannerDef )
      RScanner(this).create_missing
      RScannerCore(this,cmd).overwrite

    method on( cmd:Statements )
      forEach (element in cmd)
        if (element)
          element.dispatch( this )
          writeln
        endIf
      endForEach

    method on( cmd:Match )
      write_replacements["$scan_table"] = cmd.id

      writeln @|$scan_table.reset
               |contingent
      if (cmd.expression)
        # ScannerRead from string; must accept all characters.
        write @|  necessary ($scan_table.accept(forEach in
        write " "
        cmd.expression.dispatch(this)
        writeln @|))
      else
        # ScannerRead longest match from input
        indent += 2
        writeln @|block n=1
                 |  while (_scanner.has_another(n))
                 |    if (not $scan_table.accept(_scanner.peek(n-1)))
                 |      escapeWhile
                 |    endIf
                 |    ++n
                 |  endWhile
                 |  necessary ($scan_table.has_product)
                 |  loop ($scan_table.match_count) _scanner.read
                 |endBlock
        indent -= 2
      endIf

      writeln @|  which ($scan_table.product)
      indent += 4
      local i1 = which{ cmd._others:1 || 0 }
      forEach (statements at i in cmd.statement_lists from i1)
        writeln "case "+i
        indent += 2
        statements.dispatch(this)
        indent -= 2
      endForEach
      writeln @|others
               |  necessary (false)
      indent -= 4
      writeln @|  endWhich

      if (cmd._others)
        writeln "unsatisfied"
        indent += 2
        cmd.statement_lists[0].dispatch(this)
        indent -= 2
      endIf

      write @|endContingent

      write_replacements.remove( "$scan_table" )

    method on( cmd:ThrowSyntaxError )
      write "_throw_syntax_error"
      if (cmd.args and cmd.args.count)
        write "("
        forEach (arg at i in cmd.args)
          if (i > 0) write "+"
          cmd.args.dispatch( this )
        endForEach
        write ")"
      endIf

    method on( cmd:TokenContent )
      write "_cur_t.content"

    method on( cmd:WriteBuffer )
      write "buffer.clear.print "
      dispatch( cmd.new_value )

    method on( cmd:WriteVar )
      write "$ = " (cmd.var.name)
      dispatch( cmd.new_value )

    method on( cmd:While )
      write   "while ("
      dispatch( cmd.condition )
      writeln ")"
      indent += 2
      dispatch( cmd.statements )
      indent -= 2
      write   "endWhile"

    method write( content:String )
      if (content.contains('\n'))
        forEach (line at i in content.split('\n'))
          if (i > 0) writeln
          write( line )
        endForEach
        return
      endIf

      if (at_newline and indent)
        loop (indent) writer.print(' ')
      endIf

      if (content.contains('$'))
        content .= replacing( "$module", "module $\n" (Program.project_name) )
        forEach (entry in write_replacements.entries)
          content .= replacing( entry.key, entry.value )
        endForEach
      endIf
      writer.print( content )

      at_newline = false

    method writeln( content="":String )
      if (String.exists(content)) write( content )
      writer.println
      at_newline = true

    method on( cmd:ParserDef )
      RCmd(this).update
      RParser(this).create_missing
      RParserCore(this,cmd).overwrite
      RVisitor(this).update


    method on( cmd:Add )
      write "("
      dispatch( cmd.left )
      write " + "
      dispatch( cmd.right )
      write ")"

    method on( cmd:Subtract )
      write "("
      dispatch( cmd.left )
      write " - "
      dispatch( cmd.right )
      write ")"

    method on( cmd:Multiply )
      write "("
      dispatch( cmd.left )
      write " * "
      dispatch( cmd.right )
      write ")"

    method on( cmd:Divide )
      write "("
      dispatch( cmd.left )
      write " / "
      dispatch( cmd.right )
      write ")"

    method on( cmd:Power )
      write "("
      dispatch( cmd.left )
      write " ^ "
      dispatch( cmd.right )
      write ")"

    method on( cmd:Negate )
      write "-"
      dispatch( cmd.operand )

endClass

