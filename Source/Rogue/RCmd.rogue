module Froley

class RCmd : RogueSourceWriter
  PROPERTIES
    node_types = Set<<String>>()

  METHODS
    method init_object
      class_name = "Cmd"

    method scan_node_types( content:String )
      forEach (line in LineReader(content))
        if (line.begins_with("class "))
          local node_type = line.after_first(' ').before_first('(').before_first(' ').trimmed
          node_types.add( node_type )
        elseIf (line.contains("extern class "))
          # Write a comment containing "extern class NodeName" to prevent Froley from
          # generating a NodeName in Cmd.rogue. This allows that particular node to be
          # defined in its own file.
          local node_type = line.after_first("extern class ").before_first('(').before_first(' ').trimmed
          node_types.add( node_type )
        endIf
      endForEach

    method update_content( content:String )->String
      if (not String.exists(content))
        content =  "# Generated and updated by Froley. Custom changes will not be overwritten; customize as desired.\n"
        content += "module $\n"(Program.project_name)
      endIf

      scan_node_types( content )

      local builder = StringBuilder(content.count)
      builder.print( content )

      if (not node_types.contains("Cmd"))
        node_types.add( "Cmd" )
        builder.println ...
          @|
           |class Cmd( t:Token )
           |  GLOBAL METHODS
           |    method to_character( st:String )->Character
           |      if (st is null or st.count == 0) return Character(0)
           |      return st[0]
           |
           |  METHODS
           |    method init( existing:Cmd )
           |      t = existing.t
           |
           |    method cloned->ThisType [propagated]
           |      return ThisType(this)
           |
           |    method add( cmd:Cmd )
           |      throw UnsupportedOperationError()
           |
           |    method clear
           |      throw UnsupportedOperationError()
           |
           |    method count->Int32
           |      return 0
           |
           |    method description->String
           |      return "Cmd"
           |
           |    method dispatch<<$VisitorType>>( visitor:$VisitorType )->Cmd [propagated]
           |      return visitor.on( this )
           |
           |    method dispatch<<$VisitorType,$ReturnType>>( visitor:$VisitorType )->$ReturnType [propagated]
           |      return visitor.on( this )
           |
           |    method dispatch_on_visit<<$VisitorType>>( visitor:$VisitorType ) [propagated]
           |      visitor.on_visit( this )
           |
           |    method dispatch_on_visit_children<<$VisitorType>>( visitor:$VisitorType ) [propagated]
           |      visitor.on_visit_children( this )
           |
           |    method dispatch_on_validate<<$VisitorType>>( visitor:$VisitorType )->Cmd [propagated]
           |      return visitor.on_validate( this )
           |
           |    method first->Cmd
           |      throw UnsupportedOperationError()
           |
           |    method get( index:Int32 )->Cmd
           |      throw UnsupportedOperationError()
           |
           |    method get<<$AsType>>( index:Int32 )->$AsType
           |      return get(index)->(as $AsType)
           |
           |    method insert( element:Cmd, before_index=0:Int32 )
           |      throw UnsupportedOperationError()
           |
           |    method last->Cmd
           |      throw UnsupportedOperationError()
           |
           |    method remove_first->Cmd
           |      throw UnsupportedOperationError()
           |
           |    method remove_last->Cmd
           |      throw UnsupportedOperationError()
           |
           |    method set( index:Int32, value:Cmd )
           |      throw UnsupportedOperationError()
           |
           |    method to->Character
           |      return Character(this->Int32)
           |
           |    method to->Cmd[]
           |      throw UnsupportedOperationError()
           |
           |    method to->Int32
           |      return 0
           |
           |    method to->Logical
           |      return (this->Int32)?
           |
           |    method to->Real64
           |      return this->Int32
           |
           |    method to->String
           |      return description
           |
           |    method to->Token[]
           |      throw UnsupportedOperationError()
           |endClass
      endIf

      if (not is_node_defined("CmdList"))
        node_types.add( "CmdList" )
        builder.println ...
          @|
           |class CmdList : Cmd
           |  PROPERTIES
           |    list = Cmd[]
           |
           |  METHODS
           |    method init( t )
           |      noAction
           |
           |    method init( t, element:Cmd )
           |      list.add( element )
           |
           |    method init( t, element1:Cmd, element2:Cmd )
           |      list.add( element1 )
           |      list.add( element2 )
           |
           |    method init( existing:CmdList )
           |      prior.init( existing )
           |      list = existing.list.cloned
           |
           |    method add( cmd:Cmd )
           |      @list.add( cmd )
           |
           |    method clear
           |      @list.clear
           |
           |    method count->Int32
           |      return @list.count
           |
           |    method description->String
           |      return "CmdList" + list_description
           |
           |    method first->Cmd
           |      return @list.first
           |
           |    method get( index:Int32 )->Cmd
           |      return @list[index]
           |
           |    method insert( element:Cmd, before_index=0:Int32 )
           |      list.insert( element, before_index )
           |
           |    method last->Cmd
           |      return @list.last
           |
           |    method list_description->String
           |      use builder = StringBuilder.pool
           |        builder.print '['
           |        forEach (element at index in @list)
           |          if (index > 0) builder.print ','
           |          builder.print( %element )
           |        endForEach
           |        builder.print ']'
           |        return builder->String
           |      endUse
           |
           |    method remove_first->Cmd
           |      return @list.remove_first
           |
           |    method remove_last->Cmd
           |      return @list.remove_last
           |
           |    method set( index:Int32, value:Cmd )
           |      @list[index] = value
           |
           |    method to->Cmd[]
           |      return @list
           |
           |endClass
      endIf

      if (not is_node_defined("TokenList"))
        node_types.add( "TokenList" )
        builder.println ...
          @|
           |class TokenList : Cmd
           |  PROPERTIES
           |    tokens : Token[]
           |
           |  METHODS
           |    method init( t )
           |      tokens = Token[]
           |
           |    method init( t, tokens )
           |
           |    method init( existing:TokenList )
           |      tokens = existing.tokens.cloned
           |
           |    method description->String
           |      return "TokenList($)"(tokens)
           |
           |    method to->Token[]
           |      return tokens
           |endClass
      endIf

      if (not is_node_defined("Unary"))
        node_types.add( "Unary" )
        builder.println ...
          @|
           |class Unary( t, operand:Cmd ) : Cmd
           |  METHODS
           |    method init( existing:Unary )
           |      prior.init( existing )
           |      operand = existing.operand.cloned
           |
           |    method description->String
           |      return "Unary($)"(%operand)
           |endClass
      endIf

      if (not is_node_defined("Binary"))
        node_types.add( "Binary" )
        builder.println ...
          @|
           |class Binary( t, left:Cmd, right:Cmd ) : Cmd
           |  METHODS
           |    method init( existing:Binary )
           |      prior.init( existing )
           |      left = existing.left.cloned
           |      right = existing.right.cloned
           |
           |    method description->String
           |      return "Binary($,$)"(%left,%right)
           |endClass
      endIf

      forEach (node_type in Program.types.values.to_list.sorted((a,b)=>(a.name<b.name)))
        if (node_type.is_built_in or node_type.is_native) nextIteration

        if (not is_node_defined(node_type.name))
          node_types.add( node_type.name )

          builder.println
          builder.print( "class " ).print( node_type.name )
          if (node_type.base_type)
            builder.print( " : " ).print( node_type.base_type.name )
          endIf
          builder.println

          local extended_properties = node_type.extended_properties
          if (extended_properties.count)
            builder.println "  PROPERTIES"
            local w = 0
            w .= or_larger( (forEach in extended_properties).name.count )

            forEach (p in extended_properties)
              builder.print( "    " ).print( p.name )
              loop (w - p.name.count) builder.print ' '
              builder.print( " : " ).println( rogue_name(p.type) )
            endForEach
            builder.println
          endIf

          builder.println "  METHODS"

          if (extended_properties.count)
            # General constructors
            forEach (constructor in node_type.constructors)
              builder.print( "    method init( t" )
              if (constructor.args)
                local args = constructor.args->(as Args)
                forEach (arg in args)
                  block arg = arg->(as CreateCmdArg)
                    builder.print( ", " ).print( arg.arg_name )
                  endBlock
                endForEach
              endIf
              builder.println " )"
              builder.println
            endForEach

            # Copy contructor
            builder.print( "    method init( existing:" )
            builder.print node_type.name
            builder.println " )"
            builder.println "      prior.init( existing )"
            forEach (p in extended_properties)
              if (p.type.is_built_in or p.type.is_native)
                builder.println "      $ = existing.$"(p.name,p.name)
              else
                builder.println "      if (existing.$) $ = existing.$.cloned"(p.name,p.name,p.name)
              endIf
            endForEach
            builder.println
          endIf

          local properties = node_type.properties
          builder.println "    method description->String"
          if (node_type.is_list)
            builder.print  ''      return "$" + list_description'' (node_type.name)
          else
            if (properties.count)
              builder.print  ''      return "$('' (node_type.name)
              forEach (i of properties)
                if (i > 0) builder.print ','
                builder.print '$'
              endForEach
              builder.print ")\" ("
              forEach (p at i in properties)
                if (i > 0) builder.print ','
                builder.print '%'
                builder.print p.name
              endForEach
              builder.println ')'
            else
              builder.print  ''      return "$"'' (node_type.name)
            endIf
          endIf
          builder.println

          if (properties.count == 1 and extended_properties.count == 1)
            local name = properties.first.name
            which (properties.first.type.name)
              case "Logical"
                builder.println ''    method to->Int32:   return which{$:1 || 0}'' (name)
                builder.println ''    method to->Logical: return $'' (name)
                builder.println ''    method to->String:  return which{$:"true" || "false"}'' (name)
                builder.println
              case "Character"
                builder.println ''    method to->Character: return $'' (name)
                builder.println ''    method to->Int32:     return $'' (name)
                builder.println ''    method to->String:    return $'' (name)
                builder.println
              case "Integer"
                builder.println ''    method to->Int32:     return $'' (name)
                builder.println ''    method to->String:    return $'' (name)
                builder.println
              case "Real"
                builder.println ''    method to->Int32:  return $'' (name)
                builder.println ''    method to->Real64: return $'' (name)
                builder.println ''    method to->String: return $'' (name)
                builder.println
              case "String"
                builder.println ''    method to->Character''
                builder.println ''      return Cmd.to_character($)'' (name)
                builder.println
                builder.println ''    method to->Int32:  return $'' (name)
                builder.println ''    method to->Logical: return $?'' (name)
                builder.println ''    method to->String: return $'' (name)
                builder.println
            endWhich
          endIf

          builder.println "endClass"
        endIf
      endForEach

      content = builder->String

      return content

    method is_node_defined( name:String )->Logical
      if (node_types.contains(name)) return true
      if (File.exists(File.folder(filepath)/(name+".rogue"))) return true
      return false

endClass
