# To run this build file, install Rogue from github.com/AbePralle/Rogue and type "rogo" at the command line.

#$ ROGUEC_ARGS = --api

# Rogo is a "build your own build system" facilitator. At its core Rogo just recompiles build files if
# needed and then runs the build executable while forwarding any command line arguments. This file contains
# a default framework which uses introspection to turn command line arguments into parameterized routine calls.

# Example: to handle the command "rogo abc xyz 5", define "routine rogo_abc_xyz( n:Int32 )".

# "rogo_default" will run in the absence of any other command line argument.

augment Build
  PROPERTIES
    EXE = select{System.os=="Windows":"Build/froley.exe" || "Build/Froley-"+System.os} : String
    BINDIR   = "/usr/local/bin"
endAugment

routine rogo_default
  rogo_help
endRoutine

routine rogo_simple
  rogo_build
  execute( "cd Demos/Simple && touch Simple.froley && rogo" )
endRoutine

routine rogo_clean
  execute( "rm -rf Build" )
endRoutine

routine rogo_build
  if (Files("Source","*.rogue").add("**/*.rogue").any_newer_than(Build.EXE))
    File.create_folder( "Build" )
    println "Compiling froley"
    execute "roguec Source/Froley.rogue --output=$ --compile" (Build.EXE)
  endIf

  if (System.os != "Windows" and Process.run( "which froley", &inherit_environment ).output_string == "") install_launcher
endRoutine

routine rogo_execute( arg:String )
  execute "$ $" (Build.EXE,arg)
endRoutine

routine rogo_link
  install_launcher
endRoutine

routine rogo_unlink
  if (System.os == "Windows") return
  local launcher = Process.run( "which froley", &inherit_environment ).output_string
  if (launcher != "")
    local cmd = "rm " + launcher
    if (not execute(cmd,&suppress_error))
      println "Error deleting $ - retrying with 'sudo'." (launcher)
      #execute( "sudo " + cmd )
    endIf
  endIf
endRoutine

routine install_launcher
  if (System.os == "Windows") return
  println "-------------------------------------------------------------------------------"
  println "Creating $/froley launcher linked to $" (Build.BINDIR,Build.EXE)
  println "-------------------------------------------------------------------------------"

  local sudo = ""
  if (not File.exists(Build.BINDIR))
    println "$ does not exist. Creating..." (Build.BINDIR)
    if (not execute( "mkdir -p $" (Build.BINDIR), &suppress_error ))
      println "Error creating folder, trying again with 'sudo'"
      sudo = "sudo "
      execute( "sudo mkdir -p $" (Build.BINDIR) )
    endIf
    println "Created."
  endIf

  local folder = Process.run( "pwd" ).output_string.before_first('\n')
  local script = "#!/bin/sh\n"
  script += ''exec "$/$" "$@"\n'' (folder,Build.EXE,'$')
  File.save( ".rogo/launcher.script", script )
  if (not execute("$cp .rogo/launcher.script $/froley" (sudo,Build.BINDIR), &suppress_error=(sudo=="")))
    println "Error copying launcher script - retrying with 'sudo'."
    sudo = "sudo "
    execute "sudo cp .rogo/launcher.script $/froley" (Build.BINDIR)
  endIf
  execute "$chmod a+x $/froley" (sudo,Build.BINDIR)
  File.delete( ".rogo/launcher.script" )
endRoutine

routine execute( commands:String, &suppress_error )->Logical
  forEach (cmd in LineReader(commands))
    print( "> " ).println( cmd )
    if (System.run(cmd) != 0)
      if (suppress_error) return false
      else                throw Error( "Build failed." )
    endIf
  endForEach
  return true
endRoutine

#------------------------------------------------------------------------------
# Introspection-based Launcher Framework
#------------------------------------------------------------------------------
routine syntax( command:String, text:String )
  Build.rogo_syntax[ command ] = text
endRoutine

routine description( command:String, text:String )
  Build.rogo_descriptions[ command ] = text
endRoutine

routine help( command:String, description=null:String, syntax=null:String )
  if (description) Global.description( command, description )
  if (syntax)      Global.syntax( command, syntax )
endRoutine

try
  Build.launch
catch (err:Error)
  Build.rogo_error = err
  Build.on_error
endTry

class Build [singleton]
  PROPERTIES
    rogo_syntax         = StringTable<<String>>()
    rogo_descriptions   = StringTable<<String>>()
    rogo_prefix         = ?:{ $moduleName.count:$moduleName "::" || "" } + "rogo_" : String
    rogo_command        = "default"
    rogo_args           = @[]
    rogo_error          : Error

    LOCAL_DEFS_FILE     = "Local.mk"

  METHODS
    method launch
      rogo_args.add( forEach in System.command_line_arguments )
      read_local_defs
      on_launch
      parse_args
      dispatch_command

    method dispatch_command
      local m = find_command( rogo_command )
      require m

      local args = @[]
      forEach (arg in rogo_args)
        which (arg)
          case "true":  args.add( true )
          case "false": args.add( false )
          case "null":  args.add( NullValue )
          others:       args.add( arg )
        endWhich
      endForEach
      if (m.parameter_count == 1 and args.count > 1) args = @[ args ] # Wrap args in a ValueList.
      m.call( Global, args )

    method find_command( name:String )->MethodInfo
      return <<Global>>.find_method( rogo_prefix + name )

    method on_error
      Console.error.println rogo_error
      on_exit
      System.exit 1

    method on_command_found
      noAction

    method on_command_not_found
      println "=" * 79
      println "ERROR: No such command '$'." (rogo_args.first)
      println "=" * 79
      println
      rogo_command = "help"
      rogo_args.clear
      on_command_found

    method on_launch
      noAction

    method on_exit
      noAction

    method parse_args
      block
        if (rogo_args.count)
          local parts = String[]
          parts.add( forEach in rogo_args )
          rogo_args.clear

          while (parts.count)
            local cmd = _join( parts )
            if (find_command(cmd))
              rogo_command = cmd
              on_command_found
              escapeBlock
            endIf
            rogo_args.insert( parts.remove_last )
          endWhile

          on_command_not_found
        endIf

        # Use default command
        on_command_found
      endBlock

    method read_local_defs
      read_local_defs( LOCAL_DEFS_FILE )

    method read_local_defs( defs_filepath:String )
      # Attempt to read defs from Local.mk
      local overrides = String[]
      if (File.exists(defs_filepath))
        forEach (line in LineReader(File(defs_filepath)))
          if (line.contains("="))
            local name  = line.before_first('=').trimmed
            local value = line.after_first('=').trimmed
            if (value.begins_with('"') or value.begins_with('\''))
              value = value.leftmost(-1).rightmost(-1)
            endIf
            local p = <<Build>>.find_property( name )
            if (p)
              overrides.add( "$ = $" (name,value) )
              <<Build>>.set_property( this, p, Value(value) )
            endIf
          endIf
        endForEach
      endIf

    method _join( value:Value )->String
      local args = String[]
      args.add( forEach in value )
      return args.join( "_" )
endClass


routine rogo_help( command="":String )
  command = Build._join( Build.rogo_args )
  if (command.count)
    local syntax = get_syntax( command )
    local success = false
    if (syntax)
      println "SYNTAX"
      println "  " + syntax
      println
      success = true
    endIf
    local description = get_description( command )
    if (description)
      println "DESCRIPTION"
      forEach (line in LineReader(description.word_wrapped(76)))
        print( "  " ).println( line )
      endForEach
      println
      success = true
    endIf
    if (success)
      return
    else
      println "=" * 79
      println "ERROR: No such command '$'." (command)
      println "=" * 79
      println
    endIf
  endIf

  println "USAGE"
  local lines = String[]
  forEach (m in <<Global>>.methods)
    if (m.name.begins_with(Build.rogo_prefix))
      lines.add( "  " + get_syntax(m.name.after_first(Build.rogo_prefix)) )
    endIf
  endForEach
  lines.sort( (a,b)=>(a<b) )
  println (forEach in lines)
  println
endRoutine


routine get_syntax( m_name:String )->String
  if (Build.rogo_syntax.contains(m_name))
    return "rogo " + Build.rogo_syntax[ m_name ]
  else
    local m = <<Global>>.find_method( Build.rogo_prefix + m_name )
    if (not m) return null
    local line = "rogo $" (m_name.replacing('_',' '))
    line += " <$>" (m.parameter_name(forEach in 0..<m.parameter_count))
    return line
  endIf
endRoutine


routine get_description( m_name:String )->String
  if (Build.rogo_descriptions.contains(m_name))
    return Build.rogo_descriptions[ m_name ]
  else
    return null
  endIf
endRoutine
