library Froley

class TokenOrganizer : Visitor
  METHODS
    method on_visit( cmd:TokenGroup )->Cmd
      if (not String.exists(cmd.name)) cmd.name = "Tokens"

      visit_children( cmd )

      local entry = Program.token_groups.find( cmd.name )
      if (entry)
        if (entry is not cmd)
          # Merge this token group's definitions into existing group with the same name
          entry.value.definitions.add( forEach in cmd.definitions )
          return null
        endIf
      else
        Program.token_groups[cmd.name] = cmd
      endIf

      return cmd

    method on_visit_node( cmd:TokenDef )
      if (not cmd.symbol) cmd.symbol = cmd.name
      Program.tokens_by_name[cmd.name] = cmd
      Program.tokens_by_symbol[cmd.symbol] = cmd
      if (cmd.attributes)
        forEach (attribute in cmd.attributes)
          if (not Program.token_attributes.contains(attribute))
            Program.token_attributes[attribute] = (1:<<:Program.token_attributes.count)
          endIf
        endForEach
      endIf
      visit_children( cmd )
endClass

