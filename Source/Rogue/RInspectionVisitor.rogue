library Froley

class RInspectionVisitor : RogueSourceWriter
  PROPERTIES
    handled_node_types = Set<<String>>()

  METHODS
    method init_object
      class_name = "InspectionVisitor"

    method scan_node_types( content:String )
      forEach (line in LineReader(content))
        if (line.contains("method on_visit_children("))
          local node_type = line.after_first('(').before_first(')').after_first(':').trimmed
          handled_node_types.add( node_type )
        endIf
      endForEach

    method update_content( content:String )->String
      if (not String.exists(content) or not content.contains("class InspectionVisitor<<"))
        if (content is null) content = ""
        content +=  "# Generated and updated by Froley. Custom changes will not be overwritten; customize as desired.\n"
        content += "library $\n\n"(Program.project_name)
        content += @|class InspectionVisitor : InspectionVisitor<<Logical>>
                    |endClass
                    |
                    |class InspectionVisitor<<$ReturnType>>
                    |  # Default per-node InspectionVisitor call sequence.
                    |  #
                    |  #   CALL                                   OVERLOADABLE METHOD
                    |  #   --------------------                   ------------------------
                    |  #   visit(cmd)->$ReturnType            ->  on_visit(cmd:CmdType)->$ReturnType
                    |  #     visit_node(cmd)                  ->    on_visit_node(cmd:CmdType)
                    |  #     visit_children(cmd)->$ReturnType ->    on_visit_children(cmd:CmdType)
                    |  METHODS
                    |    method visit( cmd:Cmd )->$ReturnType [propagated]
                    |      if (cmd is null) return $ReturnType.meta.default_value
                    |      return cmd.dispatch<<on_visit,$thisType,$ReturnType>>( this )
                    |
                    |    method visit_node( cmd:Cmd ) [propagated]
                    |      cmd.dispatch<<on_visit_node,$thisType>>( this )
                    |
                    |    method visit_children( cmd:Cmd )->$ReturnType [propagated]
                    |      # Call from 'on_visit()' or 'on_visit_node()' handler to visit subtrees.
                    |      return cmd.dispatch<<on_visit_children,$thisType,$ReturnType>>( this )
                    |
                    |    method on_visit( cmd:Cmd )->$ReturnType
                    |      visit_node( cmd )
                    |      return visit_children( cmd )
                    |
                    |    method on_visit_node( cmd:Cmd )
                    |      # This method is called from the default on_visit(CmdType) before
                    |      # children are visited.
                    |
                    |    method on_visit_children( cmd:Cmd )->$ReturnType
                    |      return $ReturnType.meta.default_value
                    |
                    |    method on_visit_children( cmd:CmdList )->$ReturnType
                    |      forEach (statement in cmd.list)
                    |        local result = visit(statement)
                    |        if (result?) return result
                    |      endForEach
                    |      return $ReturnType.meta.default_value
      endIf

      scan_node_types( content )

      local builder = String(content.count*2)

      local end_class_location = content.locate( "endClass", content.locate("class InspectionVisitor<<").value )
      local bottom = "endClass\n"
      if (end_class_location)
        bottom = content.from( end_class_location.value )
        content = content.leftmost( end_class_location.value ).trimmed
      endIf

      builder.println( content )
      builder.indent = 4

      forEach (node_type in Program.types.values.to_list.sorted((a,b)=>(a.name<b.name)))
        if (node_type.is_built_in or node_type.is_native) nextIteration

        if (not handled_node_types.contains(node_type.name))
          handled_node_types.add( node_type.name )

          local properties = node_type.properties
          properties .= discarding( (p) => p.type.is_built_in or p.type.is_native )
          local extended_properties = node_type.extended_properties
          extended_properties .= discarding( (p) => p.type.is_built_in or p.type.is_native )

          if (extended_properties.count)
            local check_return = false

            builder.println
            builder.[ print("method on_visit_children( cmd:"), print(node_type.name), println(" )->$ReturnType") ]
            if (node_type.is_list and node_type.name != "CmdList")
              builder.println "  local result = on_visit_children( cmd->(as CmdList) )"
              check_return = true
            else
              builder.println "  local result : $ReturnType"
            endIf

            if (properties.count)
              forEach (p in properties)
                local type = p.type
                if (not (type.is_built_in or type.is_native))
                  if (check_return) builder.println   "  if (result?) return result"
                  builder.println   "  result = visit(cmd.$)"(p.name)
                endIf
                check_return = true
              endForEach
            endIf

            builder.println "  return result"
          endIf
        endIf
      endForEach

      builder.println

      content = builder + bottom
      return content
endClass
