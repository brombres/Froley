module F3

$include "Scanner.rogue"
$include "Parser.rogue" [optional]
$include "Type.rogue"
uses F3
uses Utility/CommandLineParser

try
  F3( System.command_line_arguments )
catch (error:SyntaxError)
  Console.error.println error
  System.exit 1
catch (error:Error)
  Console.error.println error
  #Console.error.println error.stack_trace
  System.exit 1
endTry

class F3
  PROPERTIES
    command : Value

  METHODS
    method init( args:String[] )
      command = parse_args( args )

      if (command//options//help or command//args.count != 2)
        print_usage
        System.exit 0
      endIf

      local definition_file = File(command//args.first)
      local output_folder = File(command//args.last)
      if (not definition_file.exists)
        throw Error( ''No such file "$".'' (definition_file) )
      endIf
      if (not output_folder.exists) output_folder.create_folder
      if (not output_folder.is_folder)
        throw Error( ''Unable to create folder "$".'' (output_folder) )
      endIf

      local program = Parser(definition_file).parse as Program
      if (not program)
        throw Error( ''No language definition found in file "$".'' (definition_file) )
      endIf

      program.compile( command )

    method scan_files
      forEach (arg in command//args)
        local scanner = Scanner( File(arg) )
        local tokens = scanner.tokenize
        if (scanner.output.count) print scanner.output; flush
        println (forEach in tokens)
      endForEach

    $if (defined(PARSER_EXISTS))
    method parse_files
      forEach (arg in command//args)
        local parser = Parser( File(arg) )
        local ast = parser.parse
        if (parser.output.count) print parser.output; flush
        @trace ast
      endForEach
    $endIf

    method parse_args( args:String[] )->Value
      # This method is unrelated to the Froley Parser
      local command = CommandLineParser().
      [
        option( "--main",     &alias="-m" )
        option( "--help",     &aliases=["-h","-?"] )
      ].parse( args )
      return command

    method print_usage
      println @|USAGE
               |  f3 [OPTIONS] <language-definition.froley> <output-folder>
               |
               |OPTIONS
               |  --help, -h, -?
               |    Show this help text.
               |
               |  --main, -m
               |    Creates a main application file that demonstrates how to use the scanner and parser.
               |    Does not create or modify a main file if it already exists.
endClass

