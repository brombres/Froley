module Froley

class Program [singleton]
  PROPERTIES
    command            : Value
    nodes              : ProgramDef
    project_name       : String
    output_folder      : String
    main_filename      : String

    types              = StringTable<<Type>>()
    type_Cmd           : Type
    type_CmdList       : Type

    token_groups       = StringTable<<TokenGroup>>()
    tokens_by_name     = StringTable<<TokenDef>>()
    tokens_by_symbol   = StringTable<<TokenDef>>()
    token_attributes   = StringTable<<Int32>>(){ "content":1 }

    scanner            : ScannerDef
    parser             : ParserDef

    scan_patterns      = StringTable<<String>>()
    scanner_user_calls = Set<<String>>()
    parser_user_calls  = Set<<String>>()

  METHODS
    method compile( command, nodes )
      project_name  = File.filename( command//args.first ).before_last('.')
      output_folder = File( command//args.last )

      local t = nodes.t
      type_Cmd = type(t,"Cmd").[ is_node = true ]
      type_CmdList = type(t,"CmdList").[
        set_base_type( t, type_Cmd )
        is_list=true
      ]
      type(t,"Unary").[
        set_base_type( t, type_Cmd )
        add_property( t, "operand", type_Cmd )
      ]
      type(t,"Binary").[
        set_base_type( t, type_Cmd )
        add_property( t, "left", type_Cmd )
        add_property( t, "right", type_Cmd )
      ]

      type(t,"Logical").is_built_in = true
      type(t,"Character").is_built_in = true
      type(t,"Integer").is_built_in = true
      type(t,"Real").is_built_in = true
      type(t,"String").is_built_in = true

      Collector().visit( nodes )
      TokenOrganizer().visit( nodes )
      Organizer().visit( nodes )
      Resolver().visit( nodes )
      Validator().visit( nodes )

      local main = command//options//main
      if (main)
        if (main.is_string) main_filename = main->String
        else                main_filename = project_name
      endIf
      generate_rogue( output_folder )

    method description->String
      return ""+nodes

    method generate_rogue( folder:String )
      RogueGenerator( folder )

    method type( t:Token, name:String )->Type
      local result = types[ name ]
      if (result) return result
      return Type( t, name )  # adds itself to types[]

endClass

