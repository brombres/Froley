module F2

class CmdRogueGen : RogueSourceGen
  PROPERTIES
    existing_cmd_node_types = Set<<String>>()

  METHODS
    method init_object
      class_name = "Cmd"

    method scan_node_types( content:String )
      forEach (line in LineReader(content))
        if (line.begins_with("class "))
          local node_type = line.after_first(' ').before_first('(').before_first(' ').trimmed
          existing_cmd_node_types.add( node_type )
        endIf
      endForEach

    method write_content
      local content =
        @|# Generated by Froley. Will not be overwritten; customize as desired.
         |class Cmd( t:Token )
         |  METHODS
         |    method add( cmd:Cmd )
         |      throw UnsupportedOperationError()
         |
         |    method count->Int32
         |      return 0
         |
         |    method get( index:Int32 )->Cmd
         |      throw UnsupportedOperationError()
         |endClass
         |
         |class CmdList : Cmd
         |  PROPERTIES
         |    list = Cmd[]
         |
         |  METHODS
         |    method add( cmd:Cmd )
         |      list.add( cmd )
         |
         |    method count->Int32
         |      return list.count
         |
         |    method get( index:Int32 )->Cmd
         |      return list[index]
         |endClass

      scan_node_types( content )

      # TODO: generate additional node types

      writeln content
endClass
