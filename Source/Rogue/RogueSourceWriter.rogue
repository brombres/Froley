module Froley

class RogueSourceWriter
  PROPERTIES
    generator   : RogueGenerator
    class_name  : String
    filepath    : String

  METHODS
    method init( generator )

    method indent->Int
      return generator.indent

    method close
      local content = generator.writer->String
      local file = File( filepath )
      if (file.exists)
        if (file.load_as_string != content)
          print "Recreating "
          println filepath
          file.save( content )
        endIf
      else
        print "Creating "
        println filepath
        file.save( content )
      endIf

    method create_missing
      if (File(filepath).exists) return
      overwrite( class_name )

    method filepath->String
      if (@filepath) return @filepath

      assert class_name
      local filename = class_name + ".rogue"
      filepath = generator.folder/filename
      return @filepath

    method overwrite
      assert class_name
      overwrite( class_name )

    method overwrite( class_name )
      generator.writer = String(2048)

      writeln "module $\n"(Program.project_name)
      write_content
      close

    method rogue_name( type_name:String )->String
      which (type_name)
        case "Integer": return "Int"
        case "Real":    return "Real64"
        others:         return type_name
      endWhich

    method set_indent( new_indent:Int )
      generator.indent = new_indent

    method update
      local file = File(filepath)
      if (file.exists)
        local content = update_content( file.load_as_string )
        if (content != file.load_as_string)
          print "Updating "
          println filepath
          file.save( content )
        endIf
      else
        print "Creating "
        println filepath
        file.save( update_content(null) )
      endIf

    method update_content( content:String )->String
      throw UnsupportedOperationError()  # Overridden

    method write( content:String )
      generator.write( content )

    method writeln
      generator.writeln

    method writeln( content:String )
      generator.writeln( content )

    method write_content
      noAction  # Overridden

endClass
